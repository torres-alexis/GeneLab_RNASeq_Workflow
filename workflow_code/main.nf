// main.nf
nextflow.enable.dsl=2

// Command for: 'nextflow run main.nf --version'
if (params.version) {
    println """${workflow.manifest.name}
Workflow Version: ${workflow.manifest.version}"""
    exit 0
}

// Print the pipeline version on start
println """
${workflow.manifest.name}
Workflow Version: ${workflow.manifest.version}
""".stripIndent()

// Debug warning
if (params.limit_samples_to || params.truncate_to || params.force_single_end || params.genome_subsample) {
    println("WARNING: Debugging options enabled!")
    println("Sample limit: ${params.limit_samples_to ?: 'Not set'}")
    println("Read truncation: ${params.truncate_to ? "First ${params.truncate_to} records" : 'Not set'}")
    println("Reference genome subsampling: ${params.genome_subsample ? "Region '${params.genome_subsample}'" : 'Not set'}")
    println("Force single-end analysis: ${params.force_single_end ? 'Yes' : 'No'}")
} else {
    println("No debugging options enabled")
}

include { RNASEQ } from './workflows/rnaseq.nf'
include { STAGE } from './subworkflows/stage.nf'

include { GENERATE_MD5SUMS } from './modules/generate_md5sums.nf'
include { UPDATE_ASSAY_TABLE } from './modules/update_assay_table.nf'
include { PUBLISH_STAGED_ANALYSIS } from './modules/publish_staged_analysis.nf'

ch_dp_tools_plugin = params.dp_tools_plugin ? 
    Channel.value(file(params.dp_tools_plugin)) : 
    Channel.value(file(params.mode == 'microbes' ? 
        "$projectDir/bin/dp_tools__NF_RCP_Bowtie2" : 
        "$projectDir/bin/dp_tools__NF_RCP"))

ch_accession = params.accession ? Channel.value(params.accession) : null
ch_runsheet = params.runsheet_path ? Channel.fromPath(params.runsheet_path) : null
ch_isa_archive = params.isa_archive_path ? Channel.fromPath(params.isa_archive_path) : null
ch_reference_table = Channel.value(params.reference_table)
ch_api_url = Channel.value(params.api_url)

ch_reference_store_path = Channel.value(params.reference_store_path)
ch_derived_store_path = Channel.value(params.derived_store_path)

// Set outdir based on the presence of an accession input.. currently not implemented as nextflow.config's outdir is only used for nextflow run info
//  and ch_outdir is used for all processes' publishDir in order to set either ./results or ./{accession}
ch_outdir = params.outdir ? channel.fromPath(params.outdir, checkIfExists: true) : null

workflow {
    RNASEQ(
        ch_outdir,
        ch_dp_tools_plugin,
        ch_reference_table,
        ch_accession,
        ch_isa_archive,
        ch_runsheet,
        ch_api_url,
        ch_reference_store_path,
        ch_derived_store_path
    )
}

// Workflow that only runs the initial staging steps:
//  Get the runsheet and raw reads (with debug parameters applied if applicable) and publish them to the output directory
workflow STAGE_ONLY {
    main:
        STAGE(
            ch_outdir,
            ch_dp_tools_plugin,
            ch_accession,
            ch_isa_archive,
            ch_runsheet,
            ch_api_url
        )
        PUBLISH_STAGED_ANALYSIS(
            STAGE.out.ch_outdir,
            STAGE.out.runsheet_path,
            STAGE.out.raw_reads.map { it -> it[1] }.collect()
        )
}

workflow POST_PROCESSING {
  main:
    ch_processed_directory = Channel.fromPath("${ params.outdir }/${ params.accession }", checkIfExists: true)
    ch_runsheet = Channel.fromPath("${ params.outdir }/${ params.accession }/Metadata/*_runsheet.csv", checkIfExists: true)
    UPDATE_ASSAY_TABLE(ch_processed_directory)
    GENERATE_MD5SUMS(ch_processed_directory)
}